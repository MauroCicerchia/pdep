import Personajes.*
import Hechizos.*
import Artefactos.*
import Refuerzos.*
import Universo.*

describe "Comercio"
{
	var xenia
	var thor
	var loki
	var alacachula
	var espada
	var armaduraSinRefuerzo
	var armaduraConCota
	var armaduraConBendicion
	var armaduraConAlacachula
	var espejo
	var libro
	
	fixture
	{
		xenia = new Personaje(3, 1)
		thor = new Personaje(3, 1)
		loki = new Personaje(3, 1)
		alacachula = new HechizoDeLogos("alacachula cachicomula", 1)
		espada = new Arma()
		armaduraSinRefuerzo = new Armadura(2)
		armaduraConCota = new Armadura(2)
		armaduraConCota.agregarRefuerzo(new CotaDeMalla(5))
		armaduraConBendicion = new Armadura(2)
		armaduraConBendicion.agregarRefuerzo(new Bendicion())
		armaduraConAlacachula = new Armadura(2)
		armaduraConAlacachula.agregarRefuerzo(alacachula)
		espejo = new Espejo()
		libro = new LibroDeHechizos()
		libro.agregarHechizo(alacachula)
		libro.agregarHechizo(hechizoBasico)
		xenia.hechizoPreferido(alacachula)
		thor.hechizoPreferido(hechizoBasico)
		loki.oro(5)
		loki.hechizoPreferido(hechizoBasico)
	}
	
	test "Conocer el precio de lista de un hechizo de logo cuyo nombre es “alacachula cachicomula”. 22"
	{
		assert.equals(22, alacachula.precio())
	}
	
	test "Xenia canjea el hechizo por un hechizo básico. Saber cuántas monedas le quedan. 100"
	{
		xenia.comprarHechizo(hechizoBasico)
		assert.equals(100, xenia.oro())
	}
	
	test "Thor canjea el hechizo por el hechizo de logos “alacachula cachicomula” del juego de datos. Cuántas monedas le quedan a Thor. 83"
	{
		thor.comprarHechizo(alacachula)
		assert.equals(83, thor.oro())
	}
	
	test "Hacer canjear dos veces a Thor el mismo hechizo básico. Cuántas monedas le quedan a Thor. 90"
	{
		2.times{_ => thor.comprarHechizo(hechizoBasico)}
		assert.equals(90, thor.oro())
	}
	
	test "Loki comienza con 5 monedas. Quiere canjear el hechizo por el de “alacachula cachicomula” del juego de datos. Error"
	{
		assert.throwsExceptionWithMessage("Oro insuficiente", {loki.comprarHechizo(alacachula)})
	}
	
	test "Xenia compra una espada, un collar divino (de 5 perlas), una armadura sin refuerzo y otra armadura con malla de cota cuyo valor de lucha es 5.
		Cuántas monedas le quedan a Xenia. 70.5"
	{
		xenia.comprarArtefacto(espada)
		xenia.comprarArtefacto(collarDivino)
		xenia.comprarArtefacto(armaduraSinRefuerzo)
		xenia.comprarArtefacto(armaduraConCota)
		assert.equals(70.5, xenia.oro())
	}
	
	test "Xenia compra una espada, un collar divino (de 5 perlas), una armadura sin refuerzo y otra armadura con malla de cota cuyo valor de lucha es 5.
		Cuántos artefactos tiene Xenia. 4"
	{
		xenia.comprarArtefacto(espada)
		xenia.comprarArtefacto(collarDivino)
		xenia.comprarArtefacto(armaduraSinRefuerzo)
		xenia.comprarArtefacto(armaduraConCota)
		assert.equals(4, xenia.cantidadDeArtefactos())
	}
	
	test "Thor compra una armadura con bendición como refuerzo, y una armadura reforzada con el hechizo alacachula (del juego de datos).
		Cuántas monedas le quedan a Thor. 74"
	{
		thor.comprarArtefacto(armaduraConBendicion)
		thor.comprarArtefacto(armaduraConAlacachula)
		assert.equals(74, thor.oro())
	}
	
	test "Loki quiere comprar el espejo. Error"
	{
		assert.throwsExceptionWithMessage("Oro insuficiente", {loki.comprarArtefacto(espejo)})
	}
	
	test "Xenia canjea un libro de hechizos que contiene el hechizo de logos de nombre “alacachula cachicomula” y el hechizo básico.
		Cuántas monedas le quedan a Xenia. 69"
	{
		xenia.comprarHechizo(libro)
		assert.equals(69, xenia.oro())
	}
}